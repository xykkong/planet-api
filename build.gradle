plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version "2.3.8.RELEASE"
    id 'io.spring.dependency-management' version "1.0.11.RELEASE"
    id 'com.palantir.docker' version "0.26.0"
}

group = 'com.example.starwars'

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jacoco {
    toolVersion = "0.8.6"
}

jar {
    archiveBaseName = 'planet-api'
    archiveVersion =  '0.0.1-SNAPSHOT'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task unpack(type: Copy) {
    description 'Copies the resource directory to the target directory.'
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    println "Generating docker image ${project.group}/${project.name}"
    name "${project.group}/${project.name}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'

    compileOnly 'org.projectlombok:lombok:1.18.18'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
}
